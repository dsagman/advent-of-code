 
% --- Day 1: No Time for a Taxicab ---
% Santa's sleigh uses a very high-precision clock to guide its movements, and the clock's oscillator is regulated by stars. Unfortunately, the stars have been stolen... by the Easter Bunny. To save Christmas, Santa needs you to retrieve all fifty stars by December 25th.
% Collect stars by solving puzzles. Two puzzles will be made available on each day in the Advent calendar; the second puzzle is unlocked when you complete the first. Each puzzle grants one star. Good luck!
% You're airdropped near Easter Bunny Headquarters in a city somewhere. "Near", unfortunately, is as close as you can get - the instructions on the Easter Bunny Recruiting Document the Elves intercepted start here, and nobody had time to work them out further.
% The Document indicates that you should start at the given coordinates (where you just landed) and face North. Then, follow the provided sequence: either turn left (L) or right (R) 90 degrees, then walk forward the given number of blocks, ending at a new intersection.
% There's no time to follow such ridiculous instructions on foot, though, so you take a moment and work out the destination. Given that you can only walk on the street grid of the city, how far is the shortest path to the destination?
% For example:
% Following R2, L3 leaves you 2 blocks East and 3 blocks North, or 5 blocks away.
% R2, R2, R2 leaves you 2 blocks due South of your starting position, which is 2 blocks away.
% R5, L5, R5, R3 leaves you 12 blocks away.
% How many blocks away is Easter Bunny HQ?
% Your puzzle answer was 146.
% --- Part Two ---
% Then, you notice the instructions continue on the back of the Recruiting Document. Easter Bunny HQ is actually at the first location you visit twice.
% For example, if your instructions are R8, R4, R4, R8, the first location you visit twice is 4 blocks away, due East.
% How many blocks away is the first location you visit twice?
% Your puzzle answer was 131.
% Both parts of this puzzle are complete! They provide two gold stars: **
% At this point, you should return to your Advent calendar and try another puzzle.
% If you still want to see it, you can get your puzzle input.
% You can also [Share] this puzzle.

import util.

main =>
    % Data = "R8, R4, R4, R8", % test data
    Data = to_string(read_file_chars("day.txt")),
    Dirs = [(head(S).to_lowercase,tail(S).to_int) : Xs in split(Data,","), S = strip(Xs)],
    
    (X1,Y1) = part1(Dirs),
    printf("Part 1 Answer %w\n",abs(X1)+abs(Y1)),

    (X2,Y2) = part2(Dirs),
    printf("Part 2 Answer %w\n",abs(X2)+abs(Y2)).

part1(Dirs) = Loc =>
    Facing = 0,
    Loc = (0,0),
    foreach((D,N) in Dirs)
        Facing := turn(D,Facing),
        Loc := move(Facing,Loc,N),
        % printf("Turn: %w, Now Facing: %w, Walk %w, Loc %w\n", D, Facing, N, Loc)
    end,
    (X,Y) = Loc.

part2(Dirs) = Loc => Loc = find_first_repeat(0,(0,0),[],Dirs).

find_first_repeat(_,_,_,[]) = R => R = []. % not found
find_first_repeat(Facing,Loc,Visited,[(D,N)|Ds]) = R =>
	Facing := turn(D,Facing),
	NewLocs = visits(Facing,Loc,N),
	FR = first_repeat(NewLocs,Visited),
	if (len(FR) > 0) then
		R = FR
	else
		R = find_first_repeat(Facing,last(NewLocs),Visited++NewLocs,Ds)
	end.

first_repeat([],Xs) = R => R = [].
first_repeat([H|T],Xs) = R => if (membchk(H,Xs)) then (R=H) else (R=first_repeat(T,Xs)).

% compass 0 = N, 1 = E, 2 = S, 3 = W
move(0,(X,Y),N) = (X,Y+N).
move(1,(X,Y),N) = (X+N,Y).
move(2,(X,Y),N) = (X,Y-N).
move(3,(X,Y),N) = (X-N,Y).

turn(r,D) = (D + 1) mod 4.
turn(l,D) = (D - 1) mod 4.

visits(0,(X,Y),N) = [(X,Y+I) : I in 1..N].
visits(1,(X,Y),N) = [(X+I,Y) : I in 1..N].
visits(2,(X,Y),N) = [(X,Y-I) : I in 1..N].
visits(3,(X,Y),N) = [(X-I,Y) : I in 1..N].





