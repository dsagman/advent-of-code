% https://adventofcode.com/2015/day/15
% Your recipe leaves room for exactly 100 teaspoons of ingredients. 

% The total score of a cookie can be found by adding up each of the properties (negative totals become 0) and then multiplying together everything except calories.

% For instance:

% Butterscotch: capacity -1, durability -2, flavor 6, texture 3, calories 8
% Cinnamon: capacity 2, durability 3, flavor -2, texture -1, calories 3

% Then, choosing to use 44 teaspoons of butterscotch and 56 teaspoons of cinnamon (because the amounts of each ingredient must add up to 100) would result in a cookie with the following properties:

% A capacity of 44*-1 + 56*2 = 68
% A durability of 44*-2 + 56*3 = 80
% A flavor of 44*6 + 56*-2 = 152
% A texture of 44*3 + 56*-1 = 76

% Multiplying these together (68 * 80 * 152 * 76, ignoring calories for now) results in a total score of 62842880, which happens to be the best score possible given these ingredients. If any properties had produced a negative total, it would have instead become zero, causing the whole score to multiply to zero.

% --- Part Two ---

% Your cookie recipe becomes wildly popular! Someone asks if you can make another recipe that has exactly 500 calories per cookie (so they can use it as a meal replacement). Keep the rest of your award-winning process the same (100 teaspoons, same ingredients, same scoring system).

% For example, given the ingredients above, if you had instead selected 40 teaspoons of butterscotch and 60 teaspoons of cinnamon (which still adds to 100), the total calorie count would be 40*8 + 60*3 = 500. The total score would go down, though: only 57600000, the best you can do in such trying circumstances.

% Given the ingredients in your kitchen and their properties, what is the total score of the highest-scoring cookie you can make with a calorie total of 500?

import util. 
import sat.


% https://rosettacode.org/wiki/Dot_product#Picat
dot_product(L1,L2,R) =>
    R #= max(0,sum([L1[I]*L2[I] : I in 1..L1.length])).

score(Amt,Ings,S) => 
    S #= prod([R : I in 1..Ings.length, dot_product(Amt,Ings[I],R)]).

cals(Amt,Cals,C) =>
    C #= sum([Amt[I] * Cals[I] : I in 1..Cals.length]). 

part1(Ings) = R =>
    Amt = new_array(Ings.length),
    Amt :: 1..100,
    sum(Amt) #= 100,
    score(Amt,Ings.transpose,S),
    solve($[max(S)],Amt),
    println(Amt),
    R = S.

part2(Ings, Cals) = R =>
    Amt = new_array(Ings.length),
    Amt :: 1..100,
    sum(Amt) #= 100,
    score(Amt,Ings.transpose,S),
    cals(Amt,Cals,C), % part2
    C #= 500,         % part2
    solve($[max(S)],Amt),
    println(Amt),
    R = S.

main =>
    %%%%%%%%%% test data %%%%%%%%%%5
    % Butterscotch = [-1,-2,6,3],
    % Cinnamon     = [2,3,-2,-1],
    % Ingredients  = [Butterscotch,Cinnamon],
    % Cals         = [8,3],  

    %%%%%%%%%% real data %%%%%%%%%%5
    Sprinkles    = [ 2,  0, -2,  0],
    Butterscotch = [ 0,  5, -3,  0],
    Chocolate    = [ 0,  0,  5, -1],
    Candy        = [ 0, -1,  0,  5],
    Ingredients  = [Sprinkles,Butterscotch,Chocolate,Candy],
    Cals         = [ 3,  3,  8,  8],

    P1 = part1(Ingredients),
    print("Part 1 Answer: "),
    println(P1),

    P2 = part2(Ingredients, Cals),
    print("Part 2 Answer: "),
    println(P2).